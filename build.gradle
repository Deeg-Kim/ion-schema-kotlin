buildscript {
  ext.kotlin_version = "1.2.+"

  dependencies {
    repositories { mavenCentral() }
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

group = "software.amazon.ion"
version = "alpha-0.5-SNAPSHOT"

repositories { mavenCentral() }

apply plugin: "kotlin"
dependencies {
  compile "software.amazon.ion:ion-java:1.2.+"
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}
sourceSets {
  main.java.srcDirs = ["src"]
  main.kotlin.srcDirs = ["src"]
  test.java.srcDirs = ["test"]
  test.kotlin.srcDirs = ["test"]
}

apply plugin: "checkstyle"
checkstyle {
  sourceSets = [sourceSets.main]
  ignoreFailures = false
}


apply plugin: "maven-publish"
task sourcesJar(type: Jar) {
  from "src"
  classifier = "sources"
}
task javadocJar(type: Jar) {
  from javadoc
  classifier = "javadoc"
}
publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = "Ion Schema Kotlin"
        packaging = "jar"
        url = "https://github.com/amzn/ion-schema-kotlin"
        description = "Reference implementation of the Amazon Ion Schema Specification."
        scm {
          connection = "scm:git@github.com:amzn/ion-schema-kotlin.git"
          developerConnection = "scm:git@github.com:amzn/ion-schema-kotlin.git"
          url = "git@github.com:amzn/ion-schema-kotlin.git"
        }
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = "Amazon Ion Team"
            email = "ion-team@amazon.com"
            organization = "Amazon"
            organizationUrl = "https://github.com/amzn"
          }
        }
      }
    }
  }
  repositories {
    mavenLocal()
    /*
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username ossrhUsername
        password ossrhPassword
      }
    }
    */
  }
}

apply plugin: "signing"
signing {
  sign publishing.publications.maven
}

